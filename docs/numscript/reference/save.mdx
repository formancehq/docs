---
title: Save
---

The `save` statement is used to reserve funds within a transaction, preventing them from being used in subsequent operations. It effectively sets aside a specified amount of an asset from a source account, making those funds unavailable for other operations within the same script. 

The `save` statement allows you to:
- Reserve funds in an account, preventing them from being used in subsequent transactions
- Implement minimum balance requirements
- Protect certain funds during multi-step financial operations

## Syntax

```numscript
save [asset amount] from @account
```

Where:
- `asset` is the asset type (like USD/2, EUR/2, etc.)
- `amount` is the numeric amount to save
- `@account` is the source account

## Examples

### Basic usage

```numscript
// Prevent the end balance of @merchants:1234 from going below [USD/2 100]
save [USD/2 100] from @merchants:1234

send [USD/2 500] (
  source = @merchants:1234
  destination = @payouts:T1891G
)
```

In this example, if the account `@merchants:1234` has an initial balance of `[USD/2 500]`, the transaction will fail because the post-transaction balance would be `[USD/2 0]`, which is less than the saved amount `[USD/2 100]`.

### Use multiple sources

```numscript
// Prevent the end balance of @merchants:1234 from going below [USD/2 100]
save [USD/2 100] from @merchants:1234

send [USD/2 500] (
  source = {
    @merchants:1234
    @world
  }
  destination = @payouts:T1891G
)
```

This transaction results in the following transfers:

```json
[
  {
    "source": "merchants:1234",
    "destination": "payouts:T1891G",
    "amount": 400,
    "asset": "USD/2"
  },
  {
    "source": "world",
    "destination": "payouts:T1891G",
    "amount": 100,
    "asset": "USD/2"
  }
]
```

In this case, only `[USD/2 400]` is taken from `@merchants:1234` (instead of the full `[USD/2 500]`), with the remaining `[USD/2 100]` drawn from the `@world` account.

### Save all available balance

```numscript
save [USD/2 *] from @merchants:1234

send [USD/2 30] (
   source = {
      @merchants:1234
      @world
   }
   destination = @payouts:T1891G
)
```

In this example, all USD from the merchant's account is saved. The subsequent `send` operation would need to source all 30 USD from the `@world` account since the merchant's funds are unavailable.

### Save with variables

```numscript
vars {
    monetary $amount = [USD/2 15]
}

save $amount from @merchants:1234

send [USD/2 30] (
   source = {
      @merchants:1234
      @world
   }
   destination = @payouts:T1891G
)
```

This example demonstrates using a monetary variable with the `save` statement. It saves 15 USD from the merchant's account, making that amount unavailable for the following `send` operation.
