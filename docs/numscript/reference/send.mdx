---
title: Send
---

The `send` statement is a core instruction in Numscript used to transfer assets (such as currencies or tokens) between accounts. It supports complex financial flows such as percentage-based distributions, overdraft handling, and multi-party transfers.

## Basic syntax

```numscript
send [asset amount] (
  source = @source_account
  destination = @destination_account
)
```

## Components 

Following are the key components of the `send` statement:

### Monetary value

The asset and amount to be transferred, written in square brackets using Formance's [Unambiguous Monetary Notation (UMN)](../../stack/unambiguous-monetary-notation.md).

Examples:
- `[EUR 100]` is equivalent to 100 Euros
- `[USD/2 30]` is equivalent to USD 30*1E-2, that is, USD 0.30.

### Source

Specifies where the funds come from. Commonly-used source expressions include:

- Single account: `source = @account_name`
- Variable reference: `source = @user:$userId`
- Account with overdraft: `source = @account allowing overdraft up to [asset amount]`

For complete details on source options, including multiple sources, capped sources, and allotment sources, refer to the [Sources  documentation](sources.mdx).

### Destination

Specifies where the funds go to. Basic destination expressions include:

- Single account: `destination = @account_name`
- Variable reference: `destination = @merchant:$merchantId`
- Percentage-based allocation:
  ```numscript
      destination = {
      70% to @seller:123
      remaining to @platform:fees
    }
  ```


For complete details on destination options, including nested allocations and maximum caps, refer to the [Destinations documentation](destinations.mdx).

## Account addresses

Account addresses are prefixed with `@` and can include variables in their path.

Examples:
- `@user:alice`
- `@user:$userId`
- `@accounts:savings:personal`

## Examples

### Simple transfer

```numscript
send [USD 50] (
  source = @customer:123
  destination = @merchant:456
)
```

### Transfer all available balance

Instead of specifying an exact amount, you can use the `*` wildcard to send all available balance of a specific asset:

```numscript
send [USD/2 *] (
  source = @order:1234
  destination = @merchant:5678
)
```

### Transfer using variables

```numscript
send [USD/2 100] (
  source = @customer:$customerId
  destination = @merchant:$merchantId
)
```

### Transfer with overdraft

```numscript
send [USD/2 100] (
  source = @users:1234 allowing overdraft up to [USD/2 20]
  destination = @payments:4567
)
```

### Transfer with percentage-based allocation

```numscript
send [EUR/2 120] (
  source = @order:1234
  destination = {
    10% to @platform:fees
    remaining to @merchant:5678
  }
)
```