---
title: Sources
description: Learn how to use sources in send statements
---

In Numscript, a "source" defines where funds originate from in a financial transaction. Numscript provides flexible options for specifying sources to accommodate various financial scenarios.

## Source types

### Single source

The simplest way to specify where funds come from is using a single source. This references one account that will provide the entire amount.

Syntax:

```numscript
source = @source_account
```

Example:

```numscript
send [COIN 100] (
  source = @world
  destination = @users:001
)
```

Account destinations can be specified using:
- Account references: `@account`
- Variables: `$account_variable`

### Accounts with overdraft

Numscript allows accounts to go into overdraft in two ways:

**Unbounded Overdraft**

Syntax:

```numscript
source = @source_account allowing unbounded overdraft
```

Example:

```numscript
send [COIN 100] (
  source = @source_account allowing unbounded overdraft
  destination = @users:001
)
```

This permits the account to go into unlimited negative balance.

**Bounded overdraft**

Syntax:

```numscript
source = @source_account allowing overdraft up to [ASSET AMOUNT]
```

Example:

```numscript
send [COIN 100] (
  source = @source_account allowing overdraft up to [EUR 100]
  destination = @users:001
)
```

This allows the account to go into negative balance, but only up to the specified limit.

### Allotment sources

In addition to sequential accounts, source blocks can use fractions or percentages to split the amount across multiple accounts. The summed total of fractions must equal 1, and the `remaining` keyword can be used to reach that total.

Syntax:

With fraction allocation:

```numscript
source = {
    fraction from source_account1
    remaining from source_account2
  }
```

With percentage allocation:

```numscript
source = {
    percentage% from source_account1
    remaining from source_account2
  }
```

Example:

```numscript
send [COIN 100] (
  source = {
    10% from @platform:marketing
    remaining from @users:001:wallet
  }
  destination = @orders:001
)
```

### Ordered sources

Using an ordered source block, you can define several accounts to draw from sequentially until the desired monetary value is reached.

Syntax:

```numscript
{
  source = {
    source_account1
    source_account2
  ...
  }
}
```

Example:

```numscript
send [COIN 100] (
  source = {
    @users:001:wallet
    @payments:001
  }
  destination = @orders:001
)
```

In the example above, if the balance of `COIN` in the account `@users:001:wallet` is 30, another 70 will be drawn from the `@payments:001` account.

### Capped sources

Ordered sources can be capped with a maximum amount, preventing them from being drawn more than the specified value.

Syntax:

```numscript
source = {
      max [ASSET AMOUNT] from source_account1
      source_account2
    }
```

Example:

```numscript
send [COIN 100] (
  source = {
    max [COIN 10] from @users:001:wallet
    @payments:001
  }
  destination = @orders:001
)
```

### Nested sources

Source blocks can be nested with any combination of ordered or portioned specifications.

Example:
```numscript
send [COIN 100] (
  source = {
    10% from {
      @platform:marketing
      @world
    }
    remaining from {
      @users:001:wallet
      @users:001:credit
    }
  }
  destination = @orders:001
)
```

In this example:
1. First, 10% of the total amount is taken from `@platform:marketing`
2. If that account has insufficient funds, the remainder of the 10% comes from `@world`
3. The remaining 90% is first taken from `@users:001:wallet`
4. If `@users:001:wallet` has insufficient funds, the remainder comes from `@users:001:credit`

